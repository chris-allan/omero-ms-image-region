plugins {
    id 'application'
    id 'eclipse'
    id 'java'
}

group = 'com.glencoesoftware.omero'
version = '0.4.4'

mainClassName = 'io.vertx.core.Starter'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases'
    }
    maven {
        url 'http://artifacts.openmicroscopy.org/artifactory/maven/'
    }
    maven {
        url 'https://artifacts.glencoesoftware.com/artifactory/gs-omero-snapshots-local'
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
    exclude group: 'OME'
    exclude group: 'antlr'
    exclude group: 'asm'
    exclude group: 'backport-util-concurrent'
    exclude group: 'batik'
    exclude group: 'cglib'
    exclude group: 'checkstyle'
    exclude group: 'com.codahale.metrics'
    exclude group: 'com.jamonapi'
    exclude group: 'com.mortennobel'
    exclude group: 'com.zeroc', module: 'freeze'
    exclude group: 'com.zeroc', module: 'icefreeze'
    exclude group: 'com.zeroc', module: 'icegrid'
    exclude group: 'com.zeroc', module: 'icestorm'
    exclude group: 'commons-beanutils'
    exclude group: 'commons-codec'
    exclude group: 'commons-collections'
    exclude group: 'commons-io'
    exclude group: 'commons-lang'
    exclude group: 'commons-pool'
    exclude group: 'dom4j'
    exclude group: 'edu.ucar'
    exclude group: 'freemarker'
    exclude group: 'gnu.getopt'
    exclude group: 'hsqldb'
    exclude group: 'javassist'
    exclude group: 'javax.jts'
    exclude group: 'jmock'
    exclude group: 'net.sourceforge.findbugs'
    exclude group: 'org.apache.ant'
    exclude group: 'org.apache.lucene'
    exclude group: 'org.apache.httpcomponents'
    exclude group: 'org.ini4j'
    exclude group: 'org.uncommons'
    exclude group: 'org.subethamail'
    exclude group: 'pdfbox'
    exclude group: 'quartz'
    exclude group: 'xerces'
    exclude group: 'xalan'
    exclude group: 'xml-apis'
    exclude group: 'zeroc', module: 'ice-db'
}

dependencies {
    // Upgrade from the version (3.0.2) inherited transitively which is also
    // using the deprecated `groupId` of "com.codahale.metrics"
    compile 'io.dropwizard.metrics:metrics-graphite:3.2.6'
    compile 'io.dropwizard.metrics:metrics-jvm:3.2.6'
    compile 'io.dropwizard.metrics:metrics-logback:3.2.6'
    // Upgrade from the version (1.1.1) inherited transitively
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile ('com.glencoesoftware.omero:omero-ms-core:0.4.1') {
        exclude group: 'org.testng', module: 'testng'
    }
    compile 'io.vertx:vertx-config:3.5.3'
    compile 'io.vertx:vertx-config-yaml:3.5.3'
    compile ('omero:blitz:5.4.10-ice36-b105') {
        exclude group: 'org.testng', module: 'testng'
    }
    testCompile 'org.testng:testng:6.10'
    testCompile 'org.mockito:mockito-core:2.+'
}

jar {
    manifest {
        attributes(
            "Main-Class": mainClassName,
            "Main-Verticle": "com.glencoesoftware.omero.ms.image.region.ImageRegionMicroserviceVerticle",
            "Implementation-Version": version
        )
    }
}

test {
  // enable TestNG support (default is JUnit)
  useTestNG()
}

distributions {
    main {
        contents {
            from("$projectDir") {
                include 'README.md'
            }
        }
    }
}

applicationDefaultJvmArgs = [
    "-Dvertx.logger-delegate-factory-class-name=io.vertx.core.logging.SLF4JLogDelegateFactory"
]
